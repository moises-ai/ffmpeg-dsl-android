// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'maven-publish'

def githubProperties = new Properties()

try {
    githubProperties.load(new FileInputStream(rootProject.file("github.properties")))
} catch(FileNotFoundException e) {
    // ignore
}

def user = githubProperties.getProperty('gpr.usr', System.getenv("GPR_USER"))
def key = githubProperties.getProperty('gpr.key', System.getenv("GPR_API_KEY"))
def versionName = "0.0.1"
def artificatId = "ffmpeg-dsl"

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'ai.moises'
            artifactId artificatId
            version versionName
            artifact("$projectDir/ffmpeg-dsl/build/outputs/aar/$artificatId-release.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/moises-ai/ffmpeg-dsl-android")
            credentials {
                username = user
                password = key
            }
        }
    }
}

buildscript {
    ext.kotlin_version = "1.5.0"
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter() // Warning: this repository is going to shut down soon
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}